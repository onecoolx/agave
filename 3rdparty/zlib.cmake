# agave web browser
# 
# Copyright (C) 2024 Zhang Ji Peng
# Contact: onecoolx@gmail.com

include(ExternalProject)

set(ZLIB_NAME "zlib")
set(ZLIB_VERSION "1.3.1")
set(ZLIB_PACKAGE "${PROJ_ROOT}/packages/${ZLIB_NAME}-${ZLIB_VERSION}.tar.gz")
set(ZLIB_HASH "9a93b2b7dfdac77ceba5a558a580e74667dd6fede4585b91eefb60f03b72df23")

ExternalProject_Add(
  ${ZLIB_NAME}
  PREFIX "${PROJ_OUT}/${ZLIB_NAME}"
  URL "${ZLIB_PACKAGE}"
  URL_HASH SHA256=${ZLIB_HASH}
  BUILD_IN_SOURCE
  CMAKE_ARGS -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_POSITION_INDEPENDENT_CODE=ON -DZLIB_BUILD_EXAMPLES=OFF -DCMAKE_INSTALL_PREFIX=${PROJ_OUT}
)

add_library(z STATIC IMPORTED)
if (MSVC)
set_target_properties(z PROPERTIES
  IMPORTED_LOCATION_DEBUG ${PROJ_OUT}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}${ZLIB_NAME}staticd${CMAKE_STATIC_LIBRARY_SUFFIX}
  IMPORTED_LOCATION_RELWITHDEBINFO ${PROJ_OUT}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}${ZLIB_NAME}static${CMAKE_STATIC_LIBRARY_SUFFIX}
  IMPORTED_LOCATION_RELEASE ${PROJ_OUT}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}${ZLIB_NAME}static${CMAKE_STATIC_LIBRARY_SUFFIX}
  IMPORTED_LOCATION_MINSIZEREL ${PROJ_OUT}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}${ZLIB_NAME}static${CMAKE_STATIC_LIBRARY_SUFFIX}
)
else()
set_target_properties(z PROPERTIES
  IMPORTED_LOCATION ${PROJ_OUT}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}z${CMAKE_STATIC_LIBRARY_SUFFIX}
)
endif()

set(LIB_DEPS ${LIB_DEPS} z)
